<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../program.htm">程式語言</a> - Flat Assembler (FASM) - Win32 API - <b>Create Brush</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://board.flatassembler.net/<br><br>

繪畫的顏色設定可以分成Brush、Pen兩種，Brush代表填充的顏色，Pen則是線條的顏色<br><br>

main.asm
</p>
<pre class="brush:asm">
    format PE GUI 4.0
    entry start
       
    include "c:\fasm\include\win32a.inc"
      
    section ".idata" import data readable writeable
library user, "user32.dll", kernel, "kernel32.dll", gdi, "gdi32.dll"
 
import gdi,                              \
    SetPixel,         "SetPixel",        \
    DeleteObject,     "DeleteObject",    \
    CreateSolidBrush, "CreateSolidBrush"
 
import user,                             \
    FillRect,        "FillRect",         \
    EndPaint,        "EndPaint",         \
    SetTimer,        "SetTimer",         \
    wsprintf,        "wsprintfA",        \
    KillTimer,       "KillTimer",        \
    BeginPaint,      "BeginPaint",       \
    MessageBox,      "MessageBoxA",      \
    GetMessage,      "GetMessageA",      \
    SetScrollPos,    "SetScrollPos",     \
    DestroyWindow,   "DestroyWindow",    \
    SetWindowText,   "SetWindowTextA",   \
    SetWindowLong,   "SetWindowLongA",   \
    SetScrollRange,  "SetScrollRange",   \
    CreateWindowEx,  "CreateWindowExA",  \
    CallWindowProc,  "CallWindowProcA",  \
    DispatchMessage, "DispatchMessageA", \
    PostQuitMessage, "PostQuitMessage"
      
import kernel,                           \
    ExitProcess,     "ExitProcess",      \
    GetCommandLine,  "GetCommandLineA",  \
    GetModuleHandle, "GetModuleHandleA"
       
    section ".data" data readable writeable
hWin        dd 0
szCaption   db "main",0
hInstance   dd 0
CommandLine dd 0
defWndProc  dd 0
  
    section ".text" code readable executable
proc WndProc hWnd, uMsg, wParam, lParam
    local hdc:DWORD
    local brush:DWORD
    local ps:PAINTSTRUCT
 
    mov eax, [uMsg]
    cmp eax, WM_PAINT
    je .handle_paint
    cmp eax, WM_CLOSE
    je .handle_close
    cmp eax, WM_DESTROY
    je .handle_destroy
    invoke CallWindowProc, [defWndProc], [hWnd], [uMsg], [wParam], [lParam]
    jmp .finish
  
.handle_paint:
    lea eax, [ps]
    invoke BeginPaint, [hWnd], eax
    mov [hdc], eax
 
    invoke CreateSolidBrush, 0ffh
    mov [brush], eax
    lea eax, [ps]
    add eax, PAINTSTRUCT.rcPaint
    invoke FillRect, [hdc], eax, [brush]

    lea eax, [ps]
    invoke EndPaint, [hWnd], eax
    invoke DeleteObject, [brush]
    xor eax, eax
    jmp .finish
  
.handle_close:
    invoke DestroyWindow, [hWnd]
    xor eax, eax
    jmp .finish
      
.handle_destroy:
    invoke PostQuitMessage, 0
    xor eax, eax
    jmp .finish
       
.finish:
    ret
endp
      
proc WinMain hInst, hPrevInst, CmdLine, CmdShow
    local msg:MSG
      
    invoke CreateWindowEx, WS_EX_LEFT, WC_DIALOG, szCaption, \
        WS_OVERLAPPEDWINDOW or WS_VISIBLE, 0, 0, 300, 300, NULL, NULL, NULL, NULL
    mov [hWin], eax
      
    invoke SetWindowLong, [hWin], GWL_WNDPROC, WndProc
    mov [defWndProc], eax
   
@@:
    lea eax, [msg]
    invoke GetMessage, eax, NULL, 0, 0
    cmp eax, 0
    je @f
    lea eax, [msg]
    invoke DispatchMessage, eax
    jmp @b
@@:
    mov eax, [msg.wParam]
    ret
endp
      
start:
    invoke GetModuleHandle, NULL
    mov [hInstance], eax
       
    invoke GetCommandLine
    mov [CommandLine], eax
       
    stdcall WinMain, [hInstance], NULL, [CommandLine], SW_SHOWNORMAL
    invoke ExitProcess, eax
</pre>
<p>
<b>Line 66~67</b>：產生一個紅色的Brush<br>
<b>Line 68~70</b>：將可視區域填入紅色<br>
<b>Line 74</b>：釋放資源<br><br>

完成<br>
<img style="border:0px" src="nasm_win32_brush/1.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
