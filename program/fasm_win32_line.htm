<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../program.htm">程式語言</a> - Flat Assembler (FASM) - Win32 API - <b>Draw Line</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://board.flatassembler.net/<br><br>

線的起始點位置是位於(x=0, y=0)，使用者可以呼叫MoveToEx()設定新的起始點，而使用LineTo()就可以畫出一條直線，新的起始點則是線的結束位置<br><br>

main.asm
</p>
<pre class="brush:asm">
    include "head.asm"

    section ".text" code readable executable
proc WndProc hWnd, uMsg, wParam, lParam
    local hdc:DWORD
    local pen[4]:DWORD
    local ps:PAINTSTRUCT
    
    mov eax, [uMsg]
    cmp eax, WM_PAINT
    je .handle_paint
    cmp eax, WM_CLOSE
    je .handle_close
    cmp eax, WM_DESTROY
    je .handle_destroy
    invoke CallWindowProc, [pDefWndProc], [hWnd], [uMsg], [wParam], [lParam]
    jmp .finish
     
.handle_paint:
    lea eax, [ps]
    invoke BeginPaint, [hWnd], eax
    mov [hdc], eax
    
    invoke CreatePen, PS_SOLID, 3, 0ffh
    mov [pen + 0], eax
    invoke CreatePen, PS_SOLID, 3, 0ff00h
    mov [pen + 4], eax
    invoke CreatePen, PS_SOLID, 3, 0ff0000h
    mov [pen + 8], eax
 
    invoke SelectObject, [hdc], [pen + 0]
    invoke MoveToEx, [hdc], 10, 100, NULL
    invoke LineTo, [hdc], 250, 100
   
    invoke SelectObject, [hdc], [pen + 4]
    invoke MoveToEx, [hdc], 10, 150, NULL
    invoke LineTo, [hdc], 250, 150
   
    invoke SelectObject, [hdc], [pen + 8]
    invoke MoveToEx, [hdc], 10, 200, NULL
    invoke LineTo, [hdc], 250, 200
   
    lea eax, [ps]
    invoke EndPaint, [hWnd], eax
    invoke DeleteObject, [pen + 0]
    invoke DeleteObject, [pen + 4]
    invoke DeleteObject, [pen + 8]
    xor eax, eax
    jmp .finish
     
.handle_close:
    invoke DestroyWindow, [hWnd]
    xor eax, eax
    jmp .finish
         
.handle_destroy:
    invoke PostQuitMessage, 0
    xor eax, eax
    jmp .finish
          
.finish:
    ret
endp
         
proc WinMain hInst, hPrevInst, CmdLine, CmdShow
    local msg:MSG
         
    invoke CreateWindowEx, WS_EX_LEFT, WC_DIALOG, szName, \
        WS_OVERLAPPEDWINDOW or WS_VISIBLE, 0, 0, 300, 300, NULL, NULL, NULL, NULL
    mov [hWin], eax
         
    invoke SetWindowLong, [hWin], GWL_WNDPROC, WndProc
    mov [pDefWndProc], eax
      
@@:
    lea eax, [msg]
    invoke GetMessage, eax, NULL, 0, 0
    cmp eax, 0
    je @f
    lea eax, [msg]
    invoke DispatchMessage, eax
    jmp @b
@@:
    mov eax, [msg.wParam]
    ret
endp
         
start:
    invoke GetModuleHandle, NULL
    mov [hInstance], eax
          
    invoke GetCommandLine
    mov [pCommand], eax
          
    stdcall WinMain, [hInstance], NULL, [pCommand], SW_SHOWNORMAL
    invoke ExitProcess, eax
</pre>
<p>
<b>Line 24~41</b>：產生三支Pen並且畫出三條直線，需要注意的是，同一時間只能選擇一支Pen<br><br>

完成<br>
<img style="border:0px" src="nasm_win32_line/1.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
