<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../program.htm">程式語言</a> - Flat Assembler (FASM) - Win32 API - <b>Draw Polygon</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://board.flatassembler.net/<br><br>

Polygon()可以用來繪製多邊形，需要傳入的資訊是一個座標點陣列，用來描述有哪些點需要被連接<br><br>

main.asm
</p>
<pre class="brush:asm">
    include "head.asm"

    section ".text" code readable executable
proc WndProc hWnd, uMsg, wParam, lParam
    local hdc:DWORD
    local pen:DWORD
    local brush:DWORD
    local pt[3]:POINT
    local ps:PAINTSTRUCT
       
    mov eax, [uMsg]
    cmp eax, WM_PAINT
    je .handle_paint
    cmp eax, WM_CLOSE
    je .handle_close
    cmp eax, WM_DESTROY
    je .handle_destroy
    invoke CallWindowProc, [pDefWndProc], [hWnd], [uMsg], [wParam], [lParam]
    jmp .finish
        
.handle_paint:
    lea eax, [ps]
    invoke BeginPaint, [hWnd], eax
    mov [hdc], eax
       
    invoke CreatePen, PS_SOLID, 3, 0ffh
    mov [pen], eax
    invoke CreateSolidBrush, 0ff00h
    mov [brush], eax
    
    invoke SelectObject, [hdc], [pen]
    invoke SelectObject, [hdc], [brush]
 
    mov [sizeof.POINT * 0 + pt.x], 150
    mov [sizeof.POINT * 0 + pt.y], 10
    mov [sizeof.POINT * 1 + pt.x], 50
    mov [sizeof.POINT * 1 + pt.y], 200
    mov [sizeof.POINT * 2 + pt.x], 250
    mov [sizeof.POINT * 2 + pt.y], 200
    lea eax, [pt]
    invoke Polygon, [hdc], eax, 3
      
    lea eax, [ps]
    invoke EndPaint, [hWnd], eax
    invoke DeleteObject, [pen]
    invoke DeleteObject, [brush]
    xor eax, eax
    jmp .finish
        
.handle_close:
    invoke DestroyWindow, [hWnd]
    xor eax, eax
    jmp .finish
            
.handle_destroy:
    invoke PostQuitMessage, 0
    xor eax, eax
    jmp .finish
             
.finish:
    ret
endp
            
proc WinMain hInst, hPrevInst, CmdLine, CmdShow
    local msg:MSG
            
    invoke CreateWindowEx, WS_EX_LEFT, WC_DIALOG, szName, \
        WS_OVERLAPPEDWINDOW or WS_VISIBLE, 0, 0, 300, 300, NULL, NULL, NULL, NULL
    mov [hWin], eax
            
    invoke SetWindowLong, [hWin], GWL_WNDPROC, WndProc
    mov [pDefWndProc], eax
         
@@:
    lea eax, [msg]
    invoke GetMessage, eax, NULL, 0, 0
    cmp eax, 0
    je @f
    lea eax, [msg]
    invoke DispatchMessage, eax
    jmp @b
@@:
    mov eax, [msg.wParam]
    ret
endp
            
start:
    invoke GetModuleHandle, NULL
    mov [hInstance], eax
             
    invoke GetCommandLine
    mov [pCommand], eax
             
    stdcall WinMain, [hInstance], NULL, [pCommand], SW_SHOWNORMAL
    invoke ExitProcess, eax
</pre>
<p>
<b>Line 34~41</b>：使用三個座標點畫出一個三角形<br><br>

完成<br>
<img style="border:0px" src="nasm_win32_polygon/1.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
