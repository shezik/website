<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../program.htm">程式語言</a> - Flat Assembler (FASM) - Win32 API - <b>Mouse Event</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://board.flatassembler.net/<br><br>

WM_MOUSEMOVE
</p>
<pre>
fwKeys = wParam;       // key flags
xPos = LOWORD(lParam); // horizontal position of cursor
yPos = HIWORD(lParam); // vertical position of cursor
</pre><br>

<p>
main.asm
</p>
<pre class="brush:asm">
    format PE GUI 4.0
    entry start
   
    include "c:\fasm\include\win32a.inc"
  
    section ".idata" import data readable writeable
library user, "user32.dll", kernel, "kernel32.dll"
   
import user,                             \
    wsprintf,        "wsprintfA",        \
    MessageBox,      "MessageBoxA",      \
    GetMessage,      "GetMessageA",      \
    SetWindowText,   "SetWindowTextA",   \
    DestroyWindow,   "DestroyWindow",    \
    SetWindowLong,   "SetWindowLongA",   \
    CreateWindowEx,  "CreateWindowExA",  \
    CallWindowProc,  "CallWindowProcA",  \
    DispatchMessage, "DispatchMessageA", \
    PostQuitMessage, "PostQuitMessage"
  
import kernel,                           \
    ExitProcess,     "ExitProcess",      \
    GetCommandLine,  "GetCommandLineA",  \
    GetModuleHandle, "GetModuleHandleA"
   
    section ".data" data readable writeable
hWin        dd 0
szCaption   db "main",0
hInstance   dd 0
CommandLine dd 0
defWndProc  dd 0
buf         db 255 dup(0)
fmt         db "%d-%d",0
 
    section ".text" code readable executable
proc WndProc hWnd, uMsg, wParam, lParam
    local x:DWORD
    local y:DWORD

    mov eax, [uMsg]
    cmp eax, WM_MOUSEMOVE
    je .handle_mouse
    cmp eax, WM_CLOSE
    je .handle_close
    cmp eax, WM_DESTROY
    je .handle_destroy
    invoke CallWindowProc, [defWndProc], [hWnd], [uMsg], [wParam], [lParam]
    jmp .finish
 
.handle_mouse:
    mov eax, [lParam]
    and eax, 0ffffh
    mov [x], eax

    mov eax, [lParam]
    shr eax, 16
    and eax, 0ffffh
    mov [y], eax
    invoke wsprintf, buf, fmt, [x], [y]
    invoke SetWindowText, [hWnd], buf
    xor eax, eax
    jmp .finish
 
.handle_close:
    invoke DestroyWindow, [hWnd]
    xor eax, eax
    jmp .finish
  
.handle_destroy:
    invoke PostQuitMessage, 0
    xor eax, eax
    jmp .finish
   
.finish:
    ret
endp
  
proc WinMain hInst, hPrevInst, CmdLine, CmdShow
    local msg:MSG
  
    invoke CreateWindowEx, WS_EX_LEFT, WC_DIALOG, szCaption, \
        WS_OVERLAPPEDWINDOW or WS_VISIBLE, 0, 0, 300, 300, NULL, NULL, NULL, NULL
    mov [hWin], eax
  
    invoke SetWindowLong, [hWin], GWL_WNDPROC, WndProc
    mov [defWndProc], eax
  
@@:
    lea eax, [msg]
    invoke GetMessage, eax, NULL, 0, 0
    cmp eax, 0
    je @f
    lea eax, [msg]
    invoke DispatchMessage, eax
    jmp @b
@@:
    mov eax, [msg.wParam]
    ret
endp
  
start:
    invoke GetModuleHandle, NULL
    mov [hInstance], eax
   
    invoke GetCommandLine
    mov [CommandLine], eax
   
    stdcall WinMain, [hInstance], NULL, [CommandLine], SW_SHOWNORMAL
    invoke ExitProcess, eax
</pre>
<p>
<b>Line 50~62</b>：將滑鼠座標顯示在視窗標題<br><br>

完成<br>
<img style="border:0px" src="nasm_win32_mouse/1.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
