<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../program.htm">程式語言</a> - Flat Assembler (FASM) - Win32 API - Single Document Interface (SDI) - <b>Create Window</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://board.flatassembler.net/<br><br>

Windows的視窗可以分成：<b>Dialog</b>、Single Document Interface(<b>SDI</b>)、Multiple Document Interface(<b>MDI</b>)三種類別，相對於SDI、MDI，Dialog類別是比較容易上手學習的一個類別，相當適合初次學習Windows視窗設計的使用者，而Windows視窗程式設計的核心就是圍繞着事件處理(處理Message)，任何的視窗設定或者溝通都是以事件導向為原則，因此，寫出一個簡單的視窗框架，將有助於了解Windows視窗程式的運作<br><br>

main.asm
</p>
<pre class="brush:asm">
    format PE GUI 4.0
    entry start
  
    include "c:\fasm\include\win32a.inc"
 
    section ".idata" import data readable writeable
library user, "user32.dll", kernel, "kernel32.dll"
  
import user,                            \
    MessageBox,      "MessageBoxA",     \
    GetMessage,      "GetMessageA",     \
    RegisterClass,   "RegisterClassA",  \
    DefWindowProc,   "DefWindowProcA",  \
    CreateWindowEx,  "CreateWindowExA", \
    DispatchMessage, "DispatchMessageA"
 
import kernel,                          \
    ExitProcess,     "ExitProcess",     \
    GetCommandLine,  "GetCommandLineA", \
    GetModuleHandle, "GetModuleHandleA"
  
    section ".data" data readable writeable
szName      db       "main",0
CommandLine dd       0
hInstance   dd       0
wndClass    WNDCLASS &lt;0&gt;

    section ".text" code readable executable
proc WinMain hInst, hPrevInst, CmdLine, CmdShow
    local msg:MSG

    mov eax, [DefWindowProc]
    mov [wndClass.lpfnWndProc], eax
    mov [wndClass.lpszClassName], szName
    invoke RegisterClass, wndClass
 
    invoke CreateWindowEx, WS_EX_LEFT, szName, szName, \
        WS_OVERLAPPEDWINDOW or WS_VISIBLE, 0, 0, 300, 300, NULL, NULL, NULL, NULL
 
@@:
    lea eax, [msg]
    invoke GetMessage, eax, NULL, 0, 0
    cmp eax, 0
    je @f
    lea eax, [msg]
    invoke DispatchMessage, eax
    jmp @b
@@:
    mov eax, [msg.wParam]
    ret
endp
 
start:
    invoke GetModuleHandle, NULL
    mov [hInstance], eax
  
    invoke GetCommandLine
    mov [CommandLine], eax
  
    stdcall WinMain, [hInstance], NULL, [CommandLine], SW_SHOWNORMAL
    invoke ExitProcess, eax
</pre>
<p>
<b>Line 29~51</b>：proc、endp用來定義Procedure的區間<br>
<b>Line 30</b>：Local變數宣告方式<br>
<b>Line 32~35</b>：一般SDI視窗會註冊屬於自己的Class並且設定事件處理副程式，目前司徒著重在說明如何建立Windows視窗，因此，我們使用系統預設的事件處理副程式(DefWindowProc)，視窗訊息統一交給系統幫忙處理，由於是自定義的Class，因此，需要幫這個Class設定一個名字，之後就可以用這個Class名字來建立Windows視窗<br>
<b>Line 37~38</b>：使用剛剛註冊的Class來建立Windows視窗，WS_OVERLAPPEDWINDOW代表視窗有標題、最大化、最小化的按鈕，WS_VISIBLE是建立一個可視化的視窗<br>
<b>Line 40</b>：暫時性的Label，不需要指定名稱，適合小範圍的應用，但需要搭配@f、@b使用<br>
<b>Line 41~42</b>：從Queue提取Message(包含視窗、系統事件)<br>
<b>Line 44</b>：@f(forward)代表會跳到下一個@@位置(Line 30)<br>
<b>Line 45~46</b>：分發Message到處理視窗事件的副程式，由於目前註冊的處理副程式是使用系統預設(DefWindowProc)，因此，無法處理這個SDI視窗的Message，包含關閉視窗的Message，所以必須使用kill命令強制關閉該視窗<br>
<b>Line 47</b>：@b(back)代表會跳到上一個@@位置(Line 24)<br>
<b>Line 54~58</b>：一般習慣是在程式進入時，保存Instance、CommandLine的內容<br>
<b>Line 60</b>：呼叫自定義的WinMain()，模擬C語言的WinMain()，當然也可以把內容都寫在start()裡面<br><br>

完成<br>
<img style="border:0px" src="nasm_win32_sdi_window/1.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
