<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../program.htm">程式語言</a> - Flat Assembler (FASM) - Win32 API - <b>Set Timer</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://board.flatassembler.net/<br><br>

Windows的Timer中斷週期是基於早期系統架構，因此，最短的Timer時間間隔為15ms，因此，即使Timer設定為1ms，觸發時間依舊為15ms<br><br>

main.asm
</p>
<pre class="brush:asm">
    format PE GUI 4.0
    entry start
    
    include "c:\fasm\include\win32a.inc"
   
    section ".idata" import data readable writeable
library user, "user32.dll", kernel, "kernel32.dll"
    
import user,                             \
    wsprintf,        "wsprintfA",        \
    SetTimer,        "SetTimer",         \
    KillTimer,       "KillTimer",        \
    MessageBox,      "MessageBoxA",      \
    GetMessage,      "GetMessageA",      \
    SetWindowText,   "SetWindowTextA",   \
    DestroyWindow,   "DestroyWindow",    \
    SetWindowLong,   "SetWindowLongA",   \
    CreateWindowEx,  "CreateWindowExA",  \
    CallWindowProc,  "CallWindowProcA",  \
    DispatchMessage, "DispatchMessageA", \
    PostQuitMessage, "PostQuitMessage"
   
import kernel,                           \
    ExitProcess,     "ExitProcess",      \
    GetCommandLine,  "GetCommandLineA",  \
    GetModuleHandle, "GetModuleHandleA"
    
    section ".data" data readable writeable
hWin        dd 0
szCaption   db "main",0
hInstance   dd 0
CommandLine dd 0
defWndProc  dd 0
cnt         dd 0
buf         db 255 dup(0)
fmt         db "%d",0
  
    section ".text" code readable executable
proc WndProc hWnd, uMsg, wParam, lParam
    mov eax, [uMsg]
    cmp eax, WM_TIMER
    je .handle_timer
    cmp eax, WM_CLOSE
    je .handle_close
    cmp eax, WM_DESTROY
    je .handle_destroy
    invoke CallWindowProc, [defWndProc], [hWnd], [uMsg], [wParam], [lParam]
    jmp .finish
  
.handle_timer:
    inc dword [cnt]
    invoke wsprintf, buf, fmt, [cnt]
    invoke SetWindowText, [hWnd], buf
    xor eax, eax
    jmp .finish
  
.handle_close:
    invoke DestroyWindow, [hWnd]
    xor eax, eax
    jmp .finish
   
.handle_destroy:
    invoke KillTimer, [hWnd], 1
    invoke PostQuitMessage, 0
    xor eax, eax
    jmp .finish
    
.finish:
    ret
endp
   
proc WinMain hInst, hPrevInst, CmdLine, CmdShow
    local msg:MSG
   
    invoke CreateWindowEx, WS_EX_LEFT, WC_DIALOG, szCaption, \
        WS_OVERLAPPEDWINDOW or WS_VISIBLE, 0, 0, 300, 300, NULL, NULL, NULL, NULL
    mov [hWin], eax
   
    invoke SetWindowLong, [hWin], GWL_WNDPROC, WndProc
    mov [defWndProc], eax

    invoke SetTimer, [hWin], 1, 1000, NULL
@@:
    lea eax, [msg]
    invoke GetMessage, eax, NULL, 0, 0
    cmp eax, 0
    je @f
    lea eax, [msg]
    invoke DispatchMessage, eax
    jmp @b
@@:
    mov eax, [msg.wParam]
    ret
endp
   
start:
    invoke GetModuleHandle, NULL
    mov [hInstance], eax
    
    invoke GetCommandLine
    mov [CommandLine], eax
    
    stdcall WinMain, [hInstance], NULL, [CommandLine], SW_SHOWNORMAL
    invoke ExitProcess, eax
</pre>
<p>
<b>Line 50~55</b>：累加數值並且顯示在視窗標題<br>
<b>Line 63</b>：不使用Timer，記得關閉Timer<br>
<b>Line 82</b>：設定Timer為每秒(1000ms)觸發一次，ID=1<br><br>

完成<br>
<img style="border:0px" src="nasm_win32_timer/1.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
