<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><script type="text/javascript" src="../page_head.js"></script>

<h3><a href="../driver.htm">驅動程式</a> - Windows Driver Model (WDM) - 使用範例 - BASIC (FreeBASIC) - <b>Hello, world!</b></h3>
<hr size="1"><br>
<p>
參考資訊：<br>
https://wasm.in/<br>
http://four-f.narod.ru/<br>
https://github.com/steward-fu/ddk<br><br>

main.bas
</p>
<pre class="brush:basic">
#include "wdm.bi"
 
declare function DriverEntry stdcall alias "DriverEntry" (byval pMyDriver as PDRIVER_OBJECT, byval pMyRegistry as PUNICODE_STRING) as NTSTATUS
 
dim shared pNextDevice as PDEVICE_OBJECT = Null
 
function AddDevice(byval pMyDriver as PDRIVER_OBJECT, byval pPhyDevice as PDEVICE_OBJECT) as NTSTATUS
    dim usDriverName as UNICODE_STRING
    dim pMyDevice as PDEVICE_OBJECT = null
    dim status as NTSTATUS = STATUS_SUCCESS
 
    RtlInitUnicodeString(@usDriverName, @wstr("\Device\MyDriver"))
    status = IoCreateDevice(pMyDriver, 0, @usDriverName, FILE_DEVICE_UNKNOWN, 0, FALSE, @pMyDevice)
    pNextDevice = IoAttachDeviceToDeviceStack(pMyDevice, pPhyDevice)
    (*pMyDevice).Flags = (*pMyDevice).Flags and (not DO_DEVICE_INITIALIZING)
    (*pMyDevice).Flags = (*pMyDevice).Flags or DO_BUFFERED_IO
    return STATUS_SUCCESS
end function
 
function IrpDispatch(byval pMyDevice as PDEVICE_OBJECT, byval pIrp as PIRP) as NTSTATUS
    dim pStack as PIO_STACK_LOCATION
 
    pStack = IoGetCurrentIrpStackLocation(pIrp)
    If (*pStack).MinorFunction = IRP_MN_REMOVE_DEVICE then
        IoDetachDevice(pNextDevice)
        IoDeleteDevice(pMyDevice)
        IoCompleteRequest(pIrp, IO_NO_INCREMENT)
        return STATUS_SUCCESS
    End If
     
    IoSkipCurrentIrpStackLocation(pIrp)
    return IoCallDriver(pNextDevice, pIrp)
end function
 
sub Unload(byval pMyDriver as PDRIVER_OBJECT)
end sub
  
function DriverEntry(byval pMyDriver as PDRIVER_OBJECT, byval pMyRegistry as PUNICODE_STRING) as NTSTATUS
    DbgPrint(@!"Hello, world!")
     
    (*pMyDriver).DriverUnload = @Unload
    (*pMyDriver).MajorFunction(IRP_MJ_PNP) = @IrpDispatch
    (*(*pMyDriver).DriverExtension).AddDevice = @AddDevice
    return STATUS_SUCCESS
end function
</pre>
<p>
<b>L39~44</b>：只有做Callback設定的動作，需要設定AddDevice、PNP、DriverUnload這三個Callback<br>
<b>L08~17</b>：產生一個Device Object，用來管理新加入的裝置(純軟體虛擬裝置也算)，接著初使化相關旗標<br>
<b>L21~32</b>：主要處理移除Device Object的動作，其餘Irp不處理，直接往下傳遞<br><br>

main.inf
</p>
<pre>
[Version]
Signature=$CHICAGO$
Class=Unknown
Provider=%MFGNAME%
DriverVer=8/21/2019,1.0.0.0
 
[Manufacturer]
%MFGNAME%=DeviceList
 
[DeviceList]
%DESCRIPTION%=DriverInstall, *MyDriver
 
[DestinationDirs]
DefaultDestDir=10,System32\Drivers
 
[SourceDisksFiles]
main.sys=1,,,
 
[SourceDisksNames]
1=%INSTDISK%,,,
 
[DriverInstall.NT]
CopyFiles=DriverCopyFiles
 
[DriverCopyFiles]
main.sys,,,2
 
[DriverInstall.NT.Services]
AddService=FILEIO,2,DriverService
 
[DriverService]
ServiceType=1
StartType=3
ErrorControl=1
ServiceBinary=%10%\system32\drivers\main.sys
 
[DriverInstall.NT.HW]
AddReg=DriverHwAddReg
 
[DriverHwAddReg]
HKR,,SampleInfo,,""
 
[DriverInstall]
AddReg=DriverAddReg
CopyFiles=DriverCopyFiles
 
[DriverAddReg]
HKR,,DevLoader,,*ntkern
HKR,,NTMPDriver,,main.sys
 
[DriverInstall.HW]
AddReg=DriverHwAddReg
 
[Strings]
MFGNAME="MyDriver"
INSTDISK="MyDriver Disc"
DESCRIPTION="MyDriver"
</pre><br>

<p>
編譯
</p>
<pre>
c:\freebasic\fbc32.exe -c main.bas
c:\masm32\bin\link.exe main.o /driver /base:0x10000 /subsystem:native /entry:DriverEntry "c:\masm32\lib\wxp\i386\ntoskrnl.lib" "c:\masm32\lib\wxp\i386\csq.lib" /OUT:main.sys
</pre><br>

<p>
在開始安裝驅動程式之前，需要先下載除錯工具，讓驅動程式的Debug訊息可以顯示在除錯工具上面，目前最佳的Debug輸出訊息工具是DbgView，該公司目前已經被Microsoft併購，所以可以從Microsoft網站下載，下載完後執行DbgView並將<b>Capture =&gt; Capture Kernel</b>選項打勾，接著<b>重啟</b>DbgView<br>
<img src="wdm_asm_hello/1.jpg" class="maxSize"><br><br>

對於驅動程式的安裝工具，司徒目前使用NuMega公司製作的EzDriverInstaller，將main.sys和main.inf放在同一個目錄並執行EzDriverInstaller，選擇<b>File =&gt; Open...</b>(開啟main.inf檔案)，接著按<b>Add New Device</b>就可以在DbgView上面看到輸出訊息<br>
<img src="wdm_asm_hello/2.jpg" class="maxSize"><br>
<img src="wdm_asm_hello/3.jpg" class="maxSize"><br><br>

Device Manager<br>
<img src="wdm_asm_hello/4.jpg" class="maxSize"><br><br>

Device Object<br>
<img src="wdm_asm_hello/5.jpg" class="maxSize">
</p>

<br><script type="text/javascript" src="../tail.js"></script>
