<!--
 ____________________________________________________________
|                                                            |
|    DESIGN + Pat Heard { http://fullahead.org }             |
|      DATE + 2006.09.12                                     |
| COPYRIGHT + Free use if this notice is kept in place.      |
|____________________________________________________________|

-->

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
  <title>司徒的教學網站</title>
  <meta http-lowast="content-type" content="application/xhtml+xml; charset=UTF-8" />
  <meta name="author" content="fullahead.org" />
  <meta name="keywords" content="XHTML, CSS, template, FullAhead" />
  <meta name="description" content="A valid, XHTML 1.0 template" />
  <meta name="robots" content="index, follow, noarchive" />
  <meta name="googlebot" content="noarchive" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes, minimum-scale=0.1, maximum-scale=100.0"/>

  <link rel="shortcut icon" href="../../website.ico">
  <link rel="stylesheet" type="text/css" href="../../styles/shCore.css" />
  <link rel="stylesheet" type="text/css" href="../../styles/shThemeDefault.css" />
  <link rel="stylesheet" type="text/css" href="../../css/html.css" media="screen, projection, tv " />
  <link rel="stylesheet" type="text/css" href="../../css/layout.css" media="screen, projection, tv" />
  <link rel="stylesheet" type="text/css" href="../../css/print.css" media="print" />
  
  <script type="text/javascript" src="../../scripts/shCore.js"></script>
  <script type="text/javascript" src="../../scripts/shBrushAsm.js"></script>

  <!-- Conditional comment to apply opacity fix for IE #content background.
       Invalid CSS, but can be removed without harming design -->
  <!--[if gt IE 5]>
  <link rel="stylesheet" type="text/css" href="css/ie.css" media="screen, projection, tv " />
  <![endif]-->
</head>

<body>
<script type="text/javascript">SyntaxHighlighter.all();</script>
<div id="wrapper">
<div id="content">
<script type="text/javascript" src="../../header.js"></script>
<div id="page">
<h3>Linux Device Driver &gt;&gt; Assembly (ARM)</h3>
<p><b>gpio input</b></p>
<hr size="1">
<p>
參考資訊：<br>
1. <a href="https://github.com/steward-fu/website/releases/tag/ldd">ldd</a><br><br>

由於PocketBeagle板上的Power按鍵是透過PMIC連接，不適用此篇教學，因此，需要連接另一個按鍵來使用，司徒選定27腳位，如下圖：<br>
<img src="cpp_gpio_input/2.jpg" class="maxSize"><br><br>

僅需要焊接一顆按鍵即可，連接27腳位到+3.3V<br>
<img src="cpp_gpio_input/1.jpg" class="maxSize"><br><br>

在此篇範例中，司徒使用Timer做按鍵檢查，當按鍵被按下時，點亮LED，反之則熄滅LED，因此，需要使用Linux Kernel的GPIO操作函數以及Timer，針對GPIO的輸入應用，步驟如下：<br>
1. gpio_request()<br>
2. gpiod_direction_input()<br>
3. gpiod_get_raw_value()<br>
4. gpio_free()<br><br>

ldd.S
</p>
<pre class="brush:asm">
    .global init_module
    .global cleanup_module

    .equ BUTTON,   27
    .equ USR3_LED, ((32 * 1) + 24)
    .equ PERIOD,   (100 / 4)

    .equ timer_list.entry,    0
    .equ timer_list.expires,  8
    .equ timer_list.function, 12
    .equ timer_list.data,     16
    .equ timer_list.flags,    20

    .struct 0
timer_list_s:
    i0: .struct . + 8
    i1: .struct . + 4
    i2: .struct . + 4
    i3: .struct . + 4
    i4: .struct . + 4
timer_list_e:
timer_list_l = timer_list_e - timer_list_s

    .section .modinfo, "ae"
__UNIQUE_ID_0: .asciz "license=GPL"
__UNIQUE_ID_1: .asciz "author=Steward Fu"
__UNIQUE_ID_2: .asciz "description=Linux Driver"

    .section .data
led_name: .asciz "USR3"
btn_name: .asciz "BTN"
led_desc: .dcb 4
btn_desc: .dcb 4
timer:    .space timer_list_l

    .align 2
    .section .text
timer_handler:
    push {lr}
    ldr r0, =btn_desc
    ldr r0, [r0]
    bl gpiod_get_raw_value
    mov r1, r0

    ldr r0, =led_desc
    ldr r0, [r0]
    bl gpiod_set_raw_value

    ldr r0, =timer
    ldr r1, =jiffies
    ldr r1, [r1]
    add r1, #PERIOD
    bl mod_timer
    pop {pc}

init_module:
    push {r4, lr}

    mov r0, #BUTTON
    ldr r1, =btn_name
    bl gpio_request

    mov r0, #USR3_LED
    ldr r1, =led_name
    bl gpio_request

    mov r0, #BUTTON
    bl gpio_to_desc
    mov r4, r0
    ldr r1, =btn_desc
    str r4, [r1]

    mov r0, r4
    bl gpiod_direction_input

    mov r0, #USR3_LED
    bl gpio_to_desc
    mov r4, r0
    ldr r1, =led_desc
    str r4, [r1]

    mov r0, r4
    mov r1, #1
    bl gpiod_direction_output_raw

    ldr r0, =timer
    mov r1, #0
    mov r2, #0
    mov r3, #0
    bl init_timer_key

    ldr r0, =timer
    add r0, #timer_list.function
    ldr r1, =timer_handler
    str r1, [r0]

    ldr r0, =timer
    add r0, #timer_list.data
    mov r1, #0
    str r1, [r0]

    ldr r0, =timer
    ldr r2, =jiffies
    ldr r1, [r2]
    add r1, #PERIOD
    ldr r3, =timer_handler
    bl mod_timer

    mov r0, #0
    pop {r4, pc}

cleanup_module:
    push {lr}
    ldr r0, =timer
    bl del_timer
    mov r0, #BUTTON
    bl gpio_free
    mov r0, #USR3_LED
    bl gpio_free
    pop {pc}
    .end
</pre>
<p>
<b>init_module</b>: 初始化GPIO以及Timer<br>
<b>timer_handler</b>: 取得按鍵狀態並且設定到USR3 LED<br>
<b>cleanup_module</b>: 釋放GPIO以及Timer資源<br><br>

完成<br>
<img src="cpp_gpio_input/3.jpg" class="maxSize"><br>
<img src="cpp_gpio_input/4.jpg" class="maxSize">
</p>

<p>
<br><a href="../../driver.htm">返回上一頁</a>
</p>

</div>
</div>
</div>
</body>
</html>
