<!--
 ____________________________________________________________
|                                                            |
|    DESIGN + Pat Heard { http://fullahead.org }             |
|      DATE + 2006.09.12                                     |
| COPYRIGHT + Free use if this notice is kept in place.      |
|____________________________________________________________|

-->

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
  <title>司徒的教學網站</title>
  <meta http-lowast="content-type" content="application/xhtml+xml; charset=UTF-8" />
  <meta name="author" content="fullahead.org" />
  <meta name="keywords" content="XHTML, CSS, template, FullAhead" />
  <meta name="description" content="A valid, XHTML 1.0 template" />
  <meta name="robots" content="index, follow, noarchive" />
  <meta name="googlebot" content="noarchive" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes, minimum-scale=0.1, maximum-scale=100.0"/>

  <link rel="shortcut icon" href="../../website.ico">
  <link rel="stylesheet" type="text/css" href="../../styles/shCore.css" />
  <link rel="stylesheet" type="text/css" href="../../styles/shThemeDefault.css" />
  <link rel="stylesheet" type="text/css" href="../../css/html.css" media="screen, projection, tv " />
  <link rel="stylesheet" type="text/css" href="../../css/layout.css" media="screen, projection, tv" />
  <link rel="stylesheet" type="text/css" href="../../css/print.css" media="print" />
  
  <script type="text/javascript" src="../../scripts/shCore.js"></script>
  <script type="text/javascript" src="../../scripts/shBrushAsm.js"></script>

  <!-- Conditional comment to apply opacity fix for IE #content background.
       Invalid CSS, but can be removed without harming design -->
  <!--[if gt IE 5]>
  <link rel="stylesheet" type="text/css" href="css/ie.css" media="screen, projection, tv " />
  <![endif]-->
</head>

<body>
<script type="text/javascript">SyntaxHighlighter.all();</script>
<div id="wrapper">
<div id="content">
<script type="text/javascript" src="../../header.js"></script>
<div id="page">
<h3>TRIMUI SMART &gt;&gt; Assembly</h3>
<p><b>GIC</b></p>
<hr size="1">
<p>
參考資訊：<br>
1. <a href="https://github.com/steward-fu/pdf/blob/master/s3/pm.pdf">pdf</a><br>
2. <a href="https://github.com/umanovskis/baremetal-arm">baremetal-arm</a><br><br>

Timer0的中斷號碼是50<br>
<img src="asm_gic/1.jpg" class="maxSize"><br><br>

GIC-400 DIST<br>
<img src="asm_gic/2.jpg" class="maxSize"><br><br>

GIC-400 CPUIF<br>
<img src="asm_gic/3.jpg" class="maxSize"><br><br>

main.s
</p>
<pre class="brush:asm">
    .global _start
    
    .equ SCU_BASE,   0x01c80000
    .equ GPIO_BASE,  0x01c20800
    .equ TIMER_BASE, 0x01c20c00
 
    .equ PG,                  (0x24 * 6)
    .equ TMR_IRQ_EN_REG,      0x00
    .equ TMR_IRQ_STA_REG,     0x04
    .equ TMR0_CTRL_REG,       0x10
    .equ TMR0_INTV_VALUE_REG, 0x14
    .equ TMR0_CUR_VALUE_REG,  0x18
    .equ CFG1,                0x04
    .equ DATA,                0x10

    .equ TMR0_INT,            50
    .equ GIC_DIST,            (SCU_BASE + 0x1000)
    .equ GIC_CPUIF,           (SCU_BASE + 0x2000)
    .equ DCTLR,               (GIC_DIST + 0x0000)  /* 0x0000 Distributor Control register     */
    .equ DISENABLER,          (GIC_DIST + 0x0100)  /* 0x0100 Interrupt set-enable registers   */
    .equ DITARGETSR,          (GIC_DIST + 0x0820)  /* 0x0820 Interrupt CPU targets            */
    .equ CCTLR,               (GIC_CPUIF + 0x0000) /* 0x0000 CPU Interface control register   */
    .equ CCPMR,               (GIC_CPUIF + 0x0004) /* 0x0004 Interrupt priority mask register */
    .equ CEOIR,               (GIC_CPUIF + 0x0010) /* 0x0010 End of interrupt register        */


    .arm
    .text
_start:
    .long 0xea000016
    .byte 'e', 'G', 'O', 'N', '.', 'B', 'T', '0'
    .long 0, __spl_size
    .byte 'S', 'P', 'L', 2
    .long 0, 0
    .long 0, 0, 0, 0, 0, 0, 0, 0
    .long 0, 0, 0, 0, 0, 0, 0, 0
           
_vector:
    b reset
    b .
    b .
    b .
    b .
    b .
    ldr pc, _irq
    b .

_irq: .word irq_handler

irq_handler:
    ldr r0, =GPIO_BASE
    eor r2, r3
    str r2, [r0, #(PG + DATA)]

    ldr r0, =TIMER_BASE
    ldr r1, [r0, #TMR_IRQ_STA_REG]
    str r1, [r0, #TMR_IRQ_STA_REG]
    
    ldr r0, =CEOIR
    ldr r1, =(0x3ff &amp; TMR0_INT)
    str r1, [r0]
    subs pc, lr, #4
           
reset:
    mrc p15, 0, r0, c1, c0, 0
    bic r0, #(1 &lt;&lt; 13)
    mcr p15, 0, r0, c1, c0, 0

    adr r0, _vector
    mrc p15, 0, r2, c1, c0, 0
    ands r2, r2, #(1 &lt;&lt; 13)
    ldreq r1, =0x00000000
    ldrne r1, =0xffff0000
    ldmia r0!, {r2-r8, r10}
    stmia r1!, {r2-r8, r10}
    ldmia r0!, {r2-r8, r10}
    stmia r1!, {r2-r8, r10}

    mrs r0, cpsr
    bic r0, #0x80
    msr cpsr_c, r0

    ldr r0, =CCPMR
    ldr r1, =0xffff
    str r1, [r0]

    ldr r0, =CCTLR
    ldr r1, =(1 &lt;&lt; 0)
    str r1, [r0]
    
    ldr r0, =DCTLR
    ldr r1, =(1 &lt;&lt; 0)
    str r1, [r0]

    ldr r0, =DISENABLER + ((TMR0_INT / 32) * 4)
    ldr r1, [r0]
    orr r1, #(1 &lt;&lt; (TMR0_INT % 32))
    str r1, [r0]

    ldr r0, =DITARGETSR + ((TMR0_INT / 4) * 4)
    ldr r1, [r0]
    orr r1, #(1 &lt;&lt; ((TMR0_INT % 4) * 8))
    str r1, [r0]
 
    ldr r0, =TIMER_BASE
    ldr r1, =(1 &lt;&lt; 0)
    str r1, [r0, #TMR_IRQ_EN_REG]

    ldr r1, =187500
    str r1, [r0, #TMR0_INTV_VALUE_REG]
    str r1, [r0, #TMR0_CUR_VALUE_REG]
 
    ldr r1, =(7 &lt;&lt; 4) | (1 &lt;&lt; 2) | (1 &lt;&lt; 1) | (1 &lt;&lt; 0)
    str r1, [r0, #TMR0_CTRL_REG]

    ldr r0, =GPIO_BASE
    ldr r1, =0x11111111
    str r1, [r0, #(PG + CFG1)]
     
    ldr r2, =0xffff
    ldr r3, =(1 &lt;&lt; 10)
    str r2, [r0, #(PG + DATA)]
0:
    b 0b
    .end
</pre><br>

<p>
完成<br>
<img src="asm_led/5.jpg" class="maxSize"><br>
<img src="asm_led/6.jpg" class="maxSize">
</p>

<p>
<br><a href="../../handheld.htm">返回上一頁</a>
</p>

</div>
</div>
</div>
</body>
</html>
